buildscript {
    dependencies {
        classpath("dk.toldst.dms:dms-devops-plugin:0.0.1")
    }
}

plugins {
        id 'groovy'
        id 'java'
        id 'org.springframework.boot' version '2.3.3.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'com.github.edeandrea.xjc-generation' version '1.4'
	id 'org.unbroken-dome.test-sets' version '3.0.1'
}

group = 'dk.toldst.dms.integration'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.11'

apply plugin: 'dk.toldst.dms.dms-devops-plugin'

ext {
	jaxbVersion = '2.3.1'
}

configurations {
	jaxb
	[apiElements, runtimeElements].each {
		it.outgoing.artifacts.removeIf { it.buildDependencies.getDependencies(null).contains(jar) }
		it.outgoing.artifact(bootJar)
	}
}

sourceSets {
	test {
		resources {
			srcDirs = ["src/test/resources", "src/test/jks"]
		}
	}
}

task generateJavaClasses {
	System.setProperty('javax.xml.accessExternalSchema', 'all')
	def jaxbTargetDir = file("$buildDir/generated-sources")

	doLast {
		jaxbTargetDir.mkdirs()
		ant.taskdef(
				name: 'xjc',
				classname: 'com.sun.tools.xjc.XJCTask',
				classpath: configurations.jaxb.asPath
		)
		ant.jaxbTargetDir = jaxbTargetDir
		ant.xjc(
				destdir: '${jaxbTargetDir}',
				schema: 'src/main/resources/ws/wsdl/DMSFortoldningsangivelseHent_unsecure.wsdl',
				language: 'WSDL',
				encoding: 'UTF-8'
		)
	}
}


dependencies {
	implementation("dk.toldst.dms.integration:integration-consume-service-common:0.0.1")
	implementation("dk.toldst.dms.integration:integration-security-common:0.0.1-SNAPSHOT")
	implementation 'org.springframework.boot:spring-boot-starter-web-services'
	implementation 'org.springframework.integration:spring-integration-ws'
	implementation "org.apache.commons:commons-lang3:3.0"
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'wsdl4j:wsdl4j:1.6.1'

	jaxb "com.sun.xml.bind:jaxb-xjc:$jaxbVersion"
	jaxb "com.sun.xml.bind:jaxb-impl:$jaxbVersion"
	jaxb "javax.xml.bind:jaxb-api:$jaxbVersion"
	jaxb "org.glassfish.jaxb:jaxb-core:2.3.0.1"

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.springframework.integration:spring-integration-test'
	testImplementation 'org.spockframework:spock-spring:1.2-groovy-2.5'
}

